---
title: ESP Tutorial
description: Simple tutorial for getting started with the ESP8266 and ESP32
slug: 202406-esp-tut
date: 2024-03-06
type: Post
tags:
  - embedded
  - esp
  - C++
  - tutorial
---

## Introduction

Before we delve into all the nitty-gritty details of developing embedded systems, I want to start off saying that this
is not a tutorial that yields a system that does X; instead, it focuses on how you do things, so the end product might
look lame. Still, it would be cool if you listen to me yapping for the rest of this article.

We'll start off with a small example of a blinking LED machine, and then we'll move on to more complex things. I'll be
using the ESP8266 and ESP32 for this tutorial, but the concepts are pretty much the same for other MCUs. Note that I
will not tell you how to configure your development environment, as it is different for everyone. I will be using
PlatformIO, but you can use others, such as Arduino IDE.

## LED Blinking

Controlling the integrated LED on the ESP8266 and ESP32 is pretty simple, and it can enable us to debug our code easily.
Since the LED is integrated, it can vary from board to board, but it is usually defined in the macro `BUILTIN_LED`.
Here's the code with some comments:

```cpp
#include <Arduino.h>
#include <M5StickCPlus.h> // We are using the M5StickC Plus

inline void on_led() { digitalWrite(BUILTIN_LED, HIGH); }
inline void off_led() { digitalWrite(BUILTIN_LED, LOW); }
void handle_led();
void setup() {
  // Set the LED pin as an output
  pinMode(BUILTIN_LED, OUTPUT);
}

void loop() {
  handle_led();
}

void handle_led() {
  static bool led_state = false;
  static uint32_t last = millis();
  uint32_t now = millis();
  if(now - last < 1000) {
    // If a second has not passed, the LED should be on
    if(!led_state) {
      led_state = true;
      on_led();
    }
  }else if(now - last >= 1000 && now - last <= 2000) {
    // If a second has passed, the LED should be off
    if(led_state) {
      led_state = false;
      off_led();
    }
  }else {
    last = now;
  }
}
```

Yes, it should work. You might wonder why I didn't use the `delay` function. The reason is that it is blocking, and we
want to avoid that. We want to be able to do other things while the LED is blinking. This is a simple example, but it
shows how you can use the `millis` function to create a non-blocking delay. Treat this as a _pseudo-concurrency_
example.

## Single-threaded Web Server

While the previous example was simple, this one is a bit more complex, but it's nonetheless easy to make. We will create
a simple web server that will serve a page with a button that will turn the LED on and off, with two different
mechanisms. For the sake of simplicity, we won't reinvent the wheel and use the `ESP8266WebServer.h` library for the
ESP8266. Here's the code:

```cpp
#include <Arduino.h>
#include <ESP8266WebServer.h>
#include <ESP8266WiFi.h>

constexpr char *ssid = "your_ssid";
constexpr char *password = "your_password";
// used constexpr instead of #define for C++ <3
// you can use WiFiMulti to connect to multiple networks

ESP8266WebServer server(80);
bool server_started = false; // debounce
inline void setup_wifi() { WiFi.begin(ssid, password); }

inline void create_server()
{
  server.on("/", HTTP_GET, []() {
    server.send(200, "text/html",
      "<html><body><button onclick=\"fetch('/led')\">"
      "Toggle LED</button></body></html>");
  });
  server.on("/led", HTTP_GET, []() {
    static bool led_state = false;
    led_state = !led_state;
    digitalWrite(BUILTIN_LED, led_state ? HIGH : LOW);
    server.send(200, "text/plain", led_state ? "LED is on" : "LED is off");
  });
  /**
   * Okay hear me out, before you start yelling at me for using lambda functions
    * I know that they are expensive, but in reality, they are optimized by the compiler
    * and they are not that crazily memory-hungry. I used them here for the sake of simplicity.
   */
  server.begin();
}

inline void handle_wifi() {
  if (WiFi.status() != WL_CONNECTED) {
    if(server_started) {
      server.stop(); // clean up some memory
      server_started = false;
    }
    return;
  }
  if (!server_started) {
    create_server();
    server_started = true;
  }
}

void setup()
{
  pinMode(BUILTIN_LED, OUTPUT);
  setup_wifi();
}

void loop()
{
  handle_wifi();
  if(server_started) {
    server.handleClient();
  }
}

```

"Bu-bu-but single-threaded web servers are bad!" you might say. They can not handle too many requests, but there are
times when you don't need a multi-threaded web server, especially when you have an ESP8266 that does many things at
once. If you want to use a multi-threaded web server, you can use the
[ESPAsyncWebServer](https://github.com/me-no-dev/ESPAsyncWebServer) library. On an ESP8266, it is not really necessary
to use a multi-threaded web server since it eats up a lot of memory, and the ESP8266 has only 80KB. On the other hand,
the ESP32 has 520KB, so it is more feasible to use a multi-threaded web server.

## Fun Things you can do

Here's a compilation of my ideas for fun things you can do with the ESP8266 and ESP32:

- Create a simple weather station that will display the temperature and humidity on an OLED display, additionally
  displaying real-time changes in the weather on a web page and send it to a Discord bot using a webhook.

- Create a simple home automation system that will control the lights via a web page and a mobile app. It also supports
  Apple HomeKit and Google Home.

- Create a multipurposed doorbell that can send you a notification on your phone when someone rings the doorbell, and you
  can see who it is via a camera attached to it.

- Create a spatial mapper with the AHRS sensor and display the data on a web page. Additionally, you can use
  [Three.js](https://threejs.org) to display the data in 3D. Since the web page is pretty heavy, you can host a relay
  server for it. Treat it as a small SLAM (Simultaneous Localization and Mapping) system.

## Conclusion

I hope you've learned a thing or two from this tutorial. I know it's not the panacea to all your problems, but it's a
good start. Afterall, the level of fun you have in embedded systems is inversely proportional to the level of pain
inflicted by debugging. I hope you have fun with your ESP8266 and ESP32. If you have any questions, feel free to contact
me on Discord @algasami.
